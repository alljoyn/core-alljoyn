# Copyright AllSeen Alliance. All rights reserved.
#
#    Permission to use, copy, modify, and/or distribute this software for any
#    purpose with or without fee is hereby granted, provided that the above
#    copyright notice and this permission notice appear in all copies.
#
#    THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
#    WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
#    MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
#    ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
#    WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
#    ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
#    OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
# 

Import('env')
clientDistDir = '$CPP_DISTDIR/samples/secure'

if env['OS_GROUP'] == 'windows':
    os_src = 'windows/ExclusiveFile.cc'
else:
    os_src = 'posix/ExclusiveFile.cc'

common_objs = [env.Object('ExclusiveFile', os_src), env.Object('CustomKeyStoreListener', 'CustomKeyStoreListener.cc')]

env1 = env.Clone();
env2 = env.Clone();
env1.Append(CPPDEFINES=[('CLIENT=1')])
env2.Append(CPPDEFINES=[('CLIENT=2')])

obj = env2.Object('DeskTopSharedKSClient2',  ['DeskTopSharedKSClient.cc'])

# Sample Programs
progs = [
    env.Program('DeskTopSharedKSService', [common_objs, 'DeskTopSharedKSService.cc']),
    env.Program('SampleServiceECDHE', ['SampleServiceECDHE.cc', 'SampleCertificateChainEngine.cc']),
    env.Program('SampleClientECDHE', ['SampleClientECDHE.cc', 'SampleCertificateChainEngine.cc']),
    env.Program('SampleCertificateUtility', ['SampleCertificateUtility.cc']),
    env1.Program('DeskTopSharedKSClient1',  [common_objs, 'DeskTopSharedKSClient.cc']),
    env2.Program('DeskTopSharedKSClient2',  [common_objs, obj])
    ]

# Door Sample
env.SConscript('door/SConscript', exports = {'env': env})

if env['OS'] != 'android':
     env.Install('$CPP_DISTDIR/bin/samples', progs )
     env.Install(clientDistDir, env.Glob('*.cc') + env.Glob('*.h'))
