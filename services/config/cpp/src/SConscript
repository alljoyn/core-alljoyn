# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


Import('config_env')
import os

if config_env['OS_GROUP'] == 'posix':
    config_env.Append(CXXFLAGS = ['-Wno-deprecated-declarations'])
if config_env['OS_GROUP'] == 'windows':
    config_env.Append(CXXFLAGS = ['/wd4996'])

# Sources
srcs = config_env.Glob('*.cc')

# Platform specific sources
if config_env['OS'] == 'android':
    srcs += config_env.Glob('$OS/*.cc')
else:
    srcs += config_env.Glob('$OS_GROUP/*.cc')

libs = []

# Static library
objs = config_env.Object(srcs)
libs.append(config_env.StaticLibrary('alljoyn_config', objs))

# Shared library
if config_env.get('LIBTYPE', 'static') != 'static':
    shobjs = config_env.SharedObject(srcs)
    libs.append(config_env.SharedLibrary('alljoyn_config', shobjs))

Return ('libs')